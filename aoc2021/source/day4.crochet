% crochet

// The moment I saw the game boards in this puzzle I was like, "oh,
// finally time to use some logic programming!".
//
// A significant part of this is devoted to just parsing the input,
// like always. The interesting part is the logical modelling, though.
// For this, it would be possible to do a more straight-forward modelling
// and compute winning moves at every turn. Sadly, Crochet does not currently
// support optimising incremental database changes, so we need to do some
// manual magic to prevent this from taking forever to complete.
//
// To that end, the "main" tables (logical relations) hold the game, the
// boards in that game, the input (which changes over time), and the
// numbers in the board (and whether they're marked or not).
//
// The choices of how to structure these relations are more based on how
// they're going to change and be queried as the program moves along through
// time. So for a game, we can have input changes, we need to keep track of
// the last number drawn, and we need to keep track of the winners. Both the
// input and the winners are *ordered*, so instead of storing them in a
// normalised form in the relations, they're stored as a regular Crochet list.
// Currently Crochet does not support search *in* things like lists and records,
// but that isn't a problem for this particular case.
//
// For the boards, we need to keep track of which numbers it has, whether the
// numbers are marked or not, and where those numbers lay in the board (we have
// to use this to figure out whether a row or column have been marked). We also
// keep track separately of whether the board reached a winning state (because
// we can't search in the winner list cheaply), and how many marks we have in
// each row and each column---this allows us to check for a winning move without
// having to look at every number in the board. Crochet currently does not
// support automatically generating and maintaining incremental indexes, so
// these have to be modified manually. There are no verifications that apply
// to relations yet, either, so it's hard to think about invariants of the
// database for now. That's definitely something that has to be fixed before
// a production release of Crochet.
//
// Finally, the entire puzzle is expressed as a simulation. There's one action
// here: at each turn, the game takes the next number, and everyone marks it in
// their board. This continues until there are no more numbers to draw.
//
// And there's one event: at each turn, we check which boards managed to get
// a winning number and both mark it as a winner board and append it to the
// winners list---this consequently lets us keep track of the time ordering
// for these winning boards.
//
// This simulation works for both parts, in fact. There needs to be no change
// in how they work. What changes is the simulation *goal*. Part 1 plays the
// simulation until there is one winning board (it could go on, but that would
// explore more of the simulation than strictly needed). Part 2 plays the
// simulation until there is either no input, or no non-winning boards left.
//
// At that point, each of them just uses the state of the winners list at that
// point in time, which contains all that's needed to compute the final score.

singleton day4;


// -- Parsing. It's always parsing...
type day4-data(
  global input is list<integer>,
  global boards is list<day4-board>,
);

type day4-board(
  global rows is list<list<integer>>
);

implement equality for day4-data;
command day4-data === (That is day4-data) =
  (self input === That input) and (self boards === That boards);

implement equality for day4-board;
command day4-board === (That is day4-board) =
  self rows === That rows;

command day4 parse: (Input is text) do
  let Tree = d4-grammar parse: Input;
  condition
    when Tree is error => panic message: Tree reason tag: "syntax-error";
    when Tree is ok => Tree value normalise;
  end
test
  assert (day4 parse: day4 example-input) === new day4-data(
    [7,4,9,5,11,17,23,2,0,14,21,24,10,16,13,6,15,25,12,22,18,20,8,19,3,26,1],
    [
      new day4-board([
        [22, 13, 17, 11,  0],
        [ 8,  2, 23,  4, 24],
        [21,  9, 14, 16,  7],
        [ 6, 10,  3, 18,  5],
        [ 1, 12, 20, 15, 19],
      ]),
      
      new day4-board([
        [ 3, 15,  0,  2, 22],
        [ 9, 18, 13, 17,  5],
        [19,  8,  7, 25, 23],
        [20, 11, 10, 24,  4],
        [14, 21, 16, 12,  6],
      ]),

      new day4-board([
        [14, 21, 17, 24,  4],
        [10, 16, 15,  9, 19],
        [18,  8, 23, 26, 20],
        [22, 11, 13,  6,  5],
        [ 2,  0, 12,  3,  7],
      ]),
    ]
  );
end

command d4-number normalise = #integer try-parse: self.value | value-or-panic: "not an integer";
command d4-input normalise = self.numbers map: (_ normalise);
command d4-board normalise = new day4-board(self.rows map: (_ normalise));
command d4-row normalise = self.columns map: (_ normalise);
command d4-data normalise = new day4-data(self.input normalise, self.boards map: (_ normalise));


// -- Logical modelling
relation d4-game: Game*;
relation Game* d4-game-board: Board*;
relation Game* d4-game-input: Input;
relation Game* d4-game-drawn: Number;
relation Game* d4-game-winners: Winners;
relation Board* d4-board-rows: Rows columns: Columns;
relation Board* d4-board-number: Number* marked: Boolean;
relation Board* d4-board-number: Number* row: Row column: Column;

// Manual indexes (for now)
relation Board* d4-board-won: Boolean;
relation Board* d4-board-row: Row* marks: Marks;
relation Board* d4-board-column: Column* marks: Marks;

type d4m-game;
type d4m-board;
type d4m-winner(global winning-number is integer, global board is d4m-board);

command day4 make-game: (Data is day4-data) do
  let Game = new d4m-game;
  fact d4-game: Game;
  fact Game d4-game-input: (Data input);
  fact Game d4-game-winners: [];

  // Initialising the boards
  for Board-data in Data boards do
    let Board = new d4m-board;
    fact Game d4-game-board: Board;
    fact Board d4-board-won: false;
    fact Board d4-board-rows: (Board-data rows count)
               columns: (Board-data rows first count);

    // Initialising the numbers on the boards and their "mark" status
    for Row in Board-data rows enumerate, Column in Row value enumerate do
      fact Board d4-board-number: (Column value) marked: false;
      fact Board d4-board-number: (Column value)
                 row: (Row index)
                 column: (Column index);
    end

    // Keeping track of how many marks we have on each row/column
    for Row in 1 to: Board-data rows count do
      fact Board d4-board-row: Row marks: 0;
    end

    for Column in 1 to: Board-data rows first count do
      fact Board d4-board-column: Column marks: 0;
    end
  end

  Game;
end

command d4m-board mark: (Number is integer) do
  match
    when
      self d4-board-number: Number marked: Is-marked,
      self d4-board-number: Number row: Row column: Column,
      self d4-board-row: Row marks: Row-marks,
      self d4-board-column: Column marks: Column-marks
    do
      assert not Is-marked;
      fact self d4-board-number: Number marked: true;
      fact self d4-board-row: Row marks: (Row-marks + 1);
      fact self d4-board-column: Column marks: (Column-marks + 1);
    end

    when not self d4-board-number: Number marked: _ do
      nothing
    end
  end
end

command d4m-game state =
  (search self d4-game-state: State).State first;

command d4m-game boards =
  (search self d4-game-board: Board).Board;

command d4m-game winners do
  (search self d4-game-winners: Winners).Winners first;
end

command d4m-game last-drawn =
  (search self d4-game-drawn: Number).Number first;

command d4m-winner score do
  self board unmarked-numbers sum * self winning-number;
end

command d4m-board unmarked-numbers =
  (search self d4-board-number: Number marked: false).Number;


context day4 with
  action (Game is d4m-game) step "Draw the next number"
  when
    Game d4-game-input: Input,
    if not Input is-empty,
    let Boards = search Game d4-game-board: Board
  do
    let Drawn = Input first;
    let Remaining = Input rest;
    fact Game d4-game-drawn: Drawn;
    fact Game d4-game-input: Remaining;
    Boards.Board map: (_ mark: Drawn);
  end

  when
    d4-game: Game,
    Game d4-game-drawn: Last-drawn,
    Game d4-game-winners: Winners,
    Game d4-game-board: Board,
    Board d4-board-won: false,
    Board d4-board-rows: Rows columns: Columns,
    (Board d4-board-row: _ marks: Columns | Board d4-board-column: _ marks: Rows)
  do
    fact Board d4-board-won: true;
    fact Game d4-game-winners: (Winners append: new d4m-winner(Last-drawn, Board));
  end  
end

command day4 part1: Input do
  let Data = day4 parse: Input;
  let Game = day4 make-game: Data;
  simulate for [Game] in day4 until (Game d4-game-board: Board, Board d4-board-won: true);
  Game winners first score;
test
  assert (day4 part1: day4 example-input) === 4_512;  
end

command day4 part1 do
  day4 part1: day4 input;
test
  assert day4 part1 === 8_442;
end

command day4 part2: Input do
  let Data = day4 parse: Input;
  let Game = day4 make-game: Data;
  simulate for [Game] in day4
    until (Game d4-game-input: Remaining, if Remaining is-empty)
        | not (Game d4-game-board: Board, Board d4-board-won: false);
  Game winners last score;
test
  assert (day4 part2: day4 example-input) === 1_924;
end

command day4 part2 do
  day4 part2: day4 input;
test
  assert day4 part2 === 4_590;
end